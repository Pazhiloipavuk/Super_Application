apply from: "$project.rootDir/build_scripts/dependencies.gradle"
apply from: "$project.rootDir/build_scripts/android_versions_config.gradle"

ext {
    applyDagger = { module ->
        module.dependencies {
            implementation comGoogleDaggerDependency
            kapt comGoogleKaptDaggerDependency
        }
    }

    applyAndroidModuleSettings = { module, packageName = null ->
        module.configure(module) {
            apply plugin: 'com.android.library'

            android {
                if (packageName == null) {
                    namespace "com.example.superapplication.${module.name.replace("_", "")}"
                } else {
                    namespace packageName
                }

                compileSdkVersion androidCompileSdkVersion

                defaultConfig {
                    minSdkVersion androidMinSdkVersion
                    targetSdkVersion androidTargetSdkVersion
                }

                buildTypes {
                    debug {
                        debuggable true
                    }
                }

                compileOptions {
                    sourceCompatibility javaVersion
                    targetCompatibility javaVersion
                }
            }
        }
    }

    applyKotlin = { module ->
        module.configure(module) {
            apply plugin: 'kotlin-android'
            apply plugin: 'kotlin-kapt'
            apply plugin: 'org.gradle.android.cache-fix'

            android {
                kotlinOptions {
                    jvmTarget = jvmTargetVersion
                }
            }
        }

        module.dependencies {
            implementation kotlinReflectionDependency
        }
    }
}